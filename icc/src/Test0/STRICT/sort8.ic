	/********************************************************************
	 *
	 *	Demonstration einer Paketsortiermachine
	 *	Author:	J.E. Wulff
	 *	$Id: sort8.ic,v 1.1 2004/01/05 17:44:49 jw Exp $
	 *
	 *******************************************************************/

	t100 = TIMER(TX0.4);	// Timerquelle 100 ms

	in0 = in4 & IX0.0 | ~in4 & SRT(CHANGE(IX0.0), t100, 10);
	in1 = in4 & IX0.1 | ~in4 & SRT(CHANGE(IX0.1), t100, 10);
	in2 = in4 & IX0.2 | ~in4 & SRT(CHANGE(IX0.2), t100, 10);
	in3 = in4 & IX0.3 | ~in4 & SRT(CHANGE(IX0.3), t100, 10);

	in4 = IX0.4;

	t = TIMER(CHANGE(in4 & IX0.6), ~in4 & TX0.4);	// Timerquelle manuell oder 100 ms
	c = CLOCK(CHANGE(in4 & IX0.7), ~in4 & TX0.5);	// Taktquelle manuell oder 1 Sek

	QX0.0 = tor0;	QX1.0 = m10;	QX2.0 = m18;	QX3.0 = m26;
	QX0.1 = tor1;	QX1.1 = m11;	QX2.1 = m19;	QX3.1 = m27;
	QX0.2 = tor2;	QX1.2 = m12;	QX2.2 = m20;	QX3.2 = m28;
	QX0.3 = tor3;	QX1.3 = m13;	QX2.3 = m21;	QX3.3 = m29;
	QX0.4 = m06;	QX1.4 = m14;	QX2.4 = m22;	QX3.4 = m30;
	QX0.5 = m07;	QX1.5 = m15;	QX2.5 = m23;	QX3.5 = m31;
	QX0.6 = m08;	QX1.6 = m16;	QX2.6 = m24;	QX3.6 = m32;
	QX0.7 = m09;	QX1.7 = m17;	QX2.7 = m25;	QX3.7 = m33;

	QX4.0 = m34;	QX5.0 = m42;	QX6.0 = in0;
	QX4.1 = m35;	QX5.1 = m43;	QX6.1 = in1;
	QX4.2 = m36;	QX5.2 = m44;	QX6.2 = in2;
	QX4.3 = m37;	QX5.3 = m45;	QX6.3 = in3;
	QX4.4 = m38;	QX5.4 = m46;	QX6.4 = in;
	QX4.5 = m39;	QX5.5 = m47;	QX6.5 = pip;
	QX4.6 = m40;	QX5.6 = m48;	QX6.6 = lock1;
	QX4.7 = m41;	QX5.7 = m49;	QX6.7 = lock2;

	/********************************************************************
	 *
	 *	Machinensteurung
	 *
	 *******************************************************************/

	pip = RISE((in = in0 | in1 | in2 | in3) & ~lock2, c);
	lock1 = SR(pip, m11 & m14, c);	// unlock after suitable gap
	lock2 = SR(lock1 & ~in, ~lock1);

	m06 = D(pip, c);
	m07 = D(m06 | pip & (in2 | in3), c);
	m08 = D(m07 | pip & (in1 | in3), c);
	m09 = D(m08 | pip, c);

	m10 = D(m09, c);	m20 = D(m19 & ~tor0, c);
					m30 = D(m29 & ~tor1, c);
							m40 = D(m39 & ~tor2, c);
	m11 = D(m10, c);	m21 = D(m20, c);	m31 = D(m30, c);	m41 = D(m40, c);
	m12 = D(m11, c);	m22 = D(m21, c);	m32 = D(m31, c);	m42 = D(m41, c);
	m13 = D(m12, c);	m23 = D(m22, c);	m33 = D(m32, c);	m43 = D(m42, c);
	m14 = D(m13, c);	m24 = D(m23, c);	m34 = D(m33, c);	m44 = D(m43, c);
	m15 = D(m14, c);	m25 = D(m24, c);	m35 = D(m34, c);	m45 = D(m44, c);
	m16 = D(m15, c);	m26 = D(m25, c);	m36 = D(m35, c);	m46 = D(m45, c);
	m17 = D(m16, c);	m27 = D(m26, c);	m37 = D(m36, c);	m47 = D(m46, c);
	m18 = D(m17, c);	m28 = D(m27, c);	m38 = D(m37, c);	m48 = D(m47, c);
	m19 = D(m18, c);	m29 = D(m28, c);	m39 = D(m38, c);	m49 = D(m48, c);

	tor0 = SRT( m19 & ~m18 & ~m17 &  m16, t, 5);
	tor1 = SRT( m29 &  m28 & ~m27 &  m26, t, 5);
	tor2 = SRT( m39 & ~m38 &  m37 &  m36, t, 5);
	tor3 = SRT( m49 &  m48 &  m47 &  m46, t, 5);

/**************************************************************************************************************************
 *
 * m06 m08 m10 m12 m14 m16 m18 m20 m22 m24 m26 m28 m30 m32 m34 m36 m38 m40 m42 m44 m46 m48
 *     m07 m09 m11 m13 m15 m17 m19 m21 m23 m25 m27 m29 m31 m33 m35 m37 m39 m41 m43 m45 m47 m49
 *                           lock1                tor0                                  tor1                                  tor2                                  tor3
 *
 *************************************************************************************************************************/
